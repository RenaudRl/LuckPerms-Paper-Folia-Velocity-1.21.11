plugins {
    alias(libs.plugins.licenser) apply false
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

defaultTasks 'licenseFormat', 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.cadixdev.licenser'

    group = 'me.lucko.luckperms'
    version = '5.5-SNAPSHOT'

    sourceCompatibility = 21
    targetCompatibility = 21

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events = [TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED]
            exceptionFormat = TestExceptionFormat.FULL
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    jar {
        from '../LICENSE.txt'
    }

    def determinePatchVersion = {
        try {
            // get the name of the last tag
            def tagInfo = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = tagInfo
                ignoreExitValue = true
            }
            tagInfo = tagInfo.toString().trim()

            if (!tagInfo || !tagInfo.contains('-')) {
                return 0
            }
            return tagInfo.split("-")[1]
        } catch (Exception e) {
            return 0
        }
    }

    project.ext.majorVersion = '5'
    project.ext.minorVersion = '5'
    project.ext.patchVersion = determinePatchVersion()
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://libraries.minecraft.net/' }
    }
}
